plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id "com.google.cloud.artifactregistry.gradle-plugin" version "2.1.5"
}

group 'com.testvagrant.ekam'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    //google
    implementation('com.google.inject:guice:5.0.1')

    //lombok
    implementation('org.projectlombok:lombok:1.18.20')
    annotationProcessor('org.projectlombok:lombok:1.18.20')
    testImplementation('org.projectlombok:lombok:1.18.20')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.20')

    // Test Framework
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.8.0-M1')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.8.0-M1')
    testImplementation('org.junit-pioneer:junit-pioneer:1.4.2')
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'dunzoit'
            artifactId 'ekam-config'
            version '1.0.3'

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

        }
    }

    repositories {
        maven {
            url "artifactregistry://asia-south1-maven.pkg.dev/dev-application-294611/maven-qe"
//            credentials {
//                username System.getenv("sonatypeUsername")
//                password System.getenv("sonatypePassword")
//                username sonatypeUsername
//                password sonatypePassword
//            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Ekam config to manage WEB|MOBILE|API run configuration'
            name 'Ekam Config'
            url 'https://github.com/dunzoit/ekam-config'
            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/dunzoit/ekam-config/-/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/dunzoit/ekam-config'
                connection 'https://github.com/dunzoit/ekam-config.git'
                developerConnection 'git@github.com:dunzoit/ekam-config.git'
            }
            developers {
                developer {
                    name 'Central QA'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
}

task runTests(type: Test) {
    useTestNG()
}
